; NSIS installer for VLE
; Copyright (c) 2008 The VLE Development Team
; Gauthier Quesnel <quesnel@users.sourceforge.net>
;

!include "MUI.nsh"
!include "LogicLib.nsh"


;
; General
;
Name "@VLE_NAME@"
Caption "@VLE_NAME_COMPLETE@ Setup"
!define VLE_VERSION  	"@VLE_VERSION@"
VIProductVersion	"@VLE_MAJOR@.@VLE_MINOR@.@VLE_PATCH@.0"
VIAddVersionKey         "FileDescription"       "VLE Installer"
VIAddVersionKey         "ProductName"           "@VLE_NAME@"
VIAddVersionKey         "FileVersion"           "${VLE_VERSION}"
VIAddVersionKey         "ProductVersion"        "${VLE_VERSION}"
VIAddVersionKey         "LegalCopyright"        "(C) 2003-2008 The VLE Development Team"

OutFile "@VLE_NAME_COMPLETE@-@CMAKE_SYSTEM_NAME@-@CMAKE_SYSTEM_PROCESSOR@.exe"
InstallDir "$PROGRAMFILES\VLE ${VLE_VERSION}"
InstallDirRegKey HKLM "SOFTWARE\VLE Development Team\VLE ${VLE_VERSION}" "Path"


;
; Modern UI Configuration
;
!define MUI_ABORTWARNING
!define MUI_ICON                          "@PROJECT_SOURCE_DIR@/share/pixmaps/vle.ico"
!define MUI_FINISHPAGE_NOAUTOCLOSE
!define MUI_UNFINISHPAGE_NOAUTOCLOSE

!define MUI_WELCOMEPAGE_TITLE "Welcome to the VLE @VLE_NAME_COMPLETE@ Setup Wizard"
!define MUI_WELCOMEPAGE_TEXT "This wizard will guide you through the installation of VLE (Virtual Laboratory Environment) @VLE_NAME_COMPLETE@.\r\n\r\nReport bugs to quesnel@users.sourceforge.net or on the VLE website"
!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE "@PROJECT_SOURCE_DIR@/COPYING"
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES

!define MUI_FINISHPAGE_TITLE "VLE @VLE_NAME_COMPLETE@"
!define MUI_FINISHPAGE_LINK "Visit the VLE website for the latest news, FAQs and support"
!define MUI_FINISHPAGE_LINK_LOCATION "http://vle.univ-littoral.fr"
!define MUI_FINISHPAGE_NOREBOOTSUPPORT
!insertmacro MUI_PAGE_FINISH

!insertmacro MUI_UNPAGE_WELCOME
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_UNPAGE_FINISH

!insertmacro MUI_LANGUAGE "English"


;
; Installer Sections
;
Section "VLE" SecDummy
  SetOutPath "$INSTDIR"
  File /r "@CMAKE_INSTALL_PREFIX@\*.*"
  File "@PROJECT_SOURCE_DIR@\share\pixmaps\vle.ico"
  File "@PROJECT_SOURCE_DIR@\share\pixmaps\vle.ico"
  File /r "@PROJECT_SOURCE_DIR@\COPYING"

  ; Write the path key
  WriteRegStr HKLM "SOFTWARE\VLE Development Team\VLE ${VLE_VERSION}" "Path" "$INSTDIR"
  
  ; Write the uninstall keys for Windows
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\VLE ${VLE_VERSION}" "DisplayName" "VLE ${VLE_VERSION} (remove only)"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\VLE ${VLE_VERSION}" "UninstallString" '"$INSTDIR\uninstall.exe"'
  WriteUninstaller "uninstall.exe"

  ; Environment variables: PKG_CONFIG_PATH
  ReadRegStr $0 HKLM "SYSTEM\CurrentControlSet\Control\Session Manager\Environment" "PKG_CONFIG_PATH"
  GetFullPathName /SHORT $1 "$INSTDIR"
  WriteRegStr HKLM "SYSTEM\CurrentControlSet\Control\Session Manager\Environment" "PKG_CONFIG_PATH" "$0;$1\lib\pkgconfig;"

  ; Environment variables: PATH
  ReadRegStr $0 HKLM "SYSTEM\CurrentControlSet\Control\Session Manager\Environment" "Path"
  GetFullPathName $1 "$INSTDIR"
  WriteRegStr HKLM "SYSTEM\CurrentControlSet\Control\Session Manager\Environment" "Path" "$0;$1\bin;"

  ; Associate files to vle
  WriteRegStr HKCR "vle" "" "vle Files"
  WriteRegStr HKCR "vle\DefaultIcon" "" "$INSTDIR\vle.ico"
  ReadRegStr $R0 HKCR "vle\shell\open\command" ""
  StrCmp $R0 "" 0 no_vleopen
    WriteRegStr HKCR "vle\shell" "" "open"
    WriteRegStr HKCR "vle\shell\open\command" "" '"$INSTDIR\bin\vle.exe" "%1"'
  no_vleopen:
  
  ReadRegStr $R0 HKCR ".vpz" ""
  Strcmp $R0 "vle" allready_vle no_vle
  no_vle:
    WriteRegStr HKCR ".vpz" "AM_OLD_VALUE" $R0
    WriteRegStr HKCR ".vpz" "" "vle"
  allready_vle:

  ; Shortcuts
  SetShellVarContext all
  CreateDirectory "$SMPROGRAMS\VLE ${VLE_VERSION}"
  CreateShortCut  "$SMPROGRAMS\VLE ${VLE_VERSION}\vle.lnk" "$INSTDIR\bin\vle.exe" "" "$INSTDIR\bin\vle.exe" 0
  CreateShortCut  "$SMPROGRAMS\VLE ${VLE_VERSION}\oov.lnk" "$INSTDIR\bin\oov.exe" "" "$INSTDIR\bin\oov.exe" 0
  CreateShortCut  "$SMPROGRAMS\VLE ${VLE_VERSION}\eov.lnk" "$INSTDIR\bin\eov.exe" "" "$INSTDIR\bin\eov.exe" 0
  CreateShortCut  "$SMPROGRAMS\VLE ${VLE_VERSION}\Uninstall.lnk" "$INSTDIR\uninstall.exe" "" "$INSTDIR\uninstall.exe" 0
SectionEnd


;
; Descriptions
;
!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
 !insertmacro MUI_DESCRIPTION_TEXT ${SecDummy} "Install VLE program."
!insertmacro MUI_FUNCTION_DESCRIPTION_END


;
; Unistaller Sections
;
Section "Uninstall"
  ; remove registry keys
  SetShellVarContext all
  DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\VLE ${VLE_VERSION}"
  DeleteRegValue HKLM "SOFTWARE\VLE Development Team\VLE ${VLE_VERSION}" "Path"
  DeleteRegKey HKLM "SOFTWARE\VLE Development Team\VLE ${VLE_VERSION}"
  DeleteRegKey /ifempty HKLM "SOFTWARE\VLE Development Team"
  DeleteRegKey HKCR "vle"
  RMDir /r "$SMPROGRAMS\VLE ${VLE_VERSION}"
  RMDir /r "$INSTDIR\bin"
  RMDir /r "$INSTDIR\etc"
  RMDir /r "$INSTDIR\include"
  RMDir /r "$INSTDIR\lib"
  RMDir /r "$INSTDIR\share"
  delete "$INSTDIR\uninstall.exe"
  delete "$INSTDIR\COPYING"
  delete "$INSTDIR\vle.ico"
  RMDir "$INSTDIR"

  ReadRegStr $R0 HKCR ".vpz" ""
  StrCmp $R0 "vle" 0 +3
  ReadRegStr $R0 HKCR ".vpz" "AM_OLD_VALUE"
  WriteRegStr HKCR ".vpz" "" $R0
SectionEnd
